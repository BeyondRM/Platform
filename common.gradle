//
apply plugin: 'java'
apply plugin: 'maven'

String mavenGroupId = rootProject.group
String mavenVersion = rootProject.version

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile 'abc.core:abc-cryptology:0.2'
  compile 'abc.core:abc-errorlogs:0.2'
  testCompile 'junit:junit:4.10'
}

String mavenArtifactId = name

group = mavenGroupId
version = mavenVersion

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar
  archives sourcesJar
}

configure(install.repositories.mavenInstaller) {
  pom.project {
    groupId = mavenGroupId
    artifactId = mavenArtifactId
    version = mavenVersion
  }
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
  sourceSets*.allSource*.srcDirs*.each { File srcDir ->
    if (!srcDir.isDirectory()) {
      println "Creating source folder: ${srcDir}"
      srcDir.mkdirs()
    }
  }
}
